#+ summary: Natural products per author 2
#+ description: Which authors isolated the most natural products (based on a sample of 10,000 compounds)? Return the top 30 authors by count.
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products
#+   - Author

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>

PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>

PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX bds: <http://www.bigdata.com/rdf/search#>
PREFIX gas: <http://www.bigdata.com/rdf/gas#>
PREFIX hint: <http://www.bigdata.com/queryHints#>

#title: Which authors isolated the most natural products (based on a sample of 10,000 compounds)? Return the top 30 authors by count.
#defaultView:BubbleChart
SELECT ?authorLabel ?nationalityLabel ?rgb (COUNT(DISTINCT(?compound)) AS ?count) WITH { 
  SELECT DISTINCT ?compound WHERE {
    ?compound wdt:P235 ?inchikey.
    SERVICE bd:sample { ?compound wdt:P703 ?taxon_with_compound. bd:serviceParam bd:sample.limit 10000 }  
  }
} AS %taxon_with_compound WITH { 
  SELECT DISTINCT ?compound ?art WHERE {
    INCLUDE %taxon_with_compound
            ?compound p:P703 ?stmt.
    ?stmt prov:wasDerivedFrom/pr:P248 ?art.
  }
} AS %ref WHERE {  
  INCLUDE %ref
          ?art wdt:P50 ?author.
  ?author wdt:P21 ?gender;
           wdt:P27 ?nationality.
  BIND( IF(?gender = wd:Q6581097, "2994D2", "D71D62") AS ?rgb)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?authorLabel ?nationalityLabel ?rgb
ORDER BY DESC (?count)
LIMIT 30