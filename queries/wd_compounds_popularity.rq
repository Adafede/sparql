#+ summary: Natural Products Popularity
#+ description: Which are the most popular natural products based on the number of sitelinks on their corresponding QID?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products
#+   - Popularity

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>

PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>

PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX bds: <http://www.bigdata.com/rdf/search#>
PREFIX gas: <http://www.bigdata.com/rdf/gas#>
PREFIX hint: <http://www.bigdata.com/queryHints#>

#title: Which are the most popular natural products based on the number of sitelinks on their corresponding QID?
SELECT DISTINCT ?np ?sitelinks
with {
  select distinct ?sitelinks {
    [] wikibase:sitelinks ?sitelinks.
  }
} as %values
WHERE {
  hint:Query hint:optimizer "None".
  VALUES ?instances {
    wd:Q113145171 # type of a chemical entity
    wd:Q59199015 # group of stereoisomers
  }

  include %values.
  filter (?sitelinks > 90).
  
  ?np wikibase:sitelinks ?sitelinks;
    wdt:P31 ?instances;
    (p:P703/ps:P703) ?t; # This filters chemicals found in a taxon as a proxy for "natural product"
      wdt:P2067 ?mass
      
  filter (?mass > 100) # removes compounds such as water, ethanol, etc.
}
ORDER BY DESC (?sitelinks)