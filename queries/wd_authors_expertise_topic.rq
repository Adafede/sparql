#+ summary: Women experts proteomics
#+ description: Women with expertise in proteomics
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Expertise
#+   - Proteomics
#+   - Women
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wds: <http://www.wikidata.org/entity/statement/>
PREFIX wdv: <http://www.wikidata.org/value/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>

PREFIX wdref: <http://www.wikidata.org/reference/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX psn: <http://www.wikidata.org/prop/statement/value-normalized/>
PREFIX pqv: <http://www.wikidata.org/prop/qualifier/value/>
PREFIX pqn: <http://www.wikidata.org/prop/qualifier/value-normalized/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prv: <http://www.wikidata.org/prop/reference/value/>
PREFIX prn: <http://www.wikidata.org/prop/reference/value-normalized/>
PREFIX wdno: <http://www.wikidata.org/prop/novalue/>
PREFIX wdata: <http://www.wikidata.org/wiki/Special:EntityData/>

PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX bds: <http://www.bigdata.com/rdf/search#>
PREFIX gas: <http://www.bigdata.com/rdf/gas#>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: Women with expertise in proteomics
SELECT * WITH {
  SELECT DISTINCT * WHERE {
    VALUES ?subject {
      wd:Q471857
      wd:Q420
      wd:Q177005
      wd:Q30612
    }
    {?scholar ^wdt:P50/wdt:P921 ?subject.}
    UNION
    {?scholar wdt:P101 ?subject.}
    ?scholar wdt:P21 wd:Q6581072.
  }
} AS %SCHOLARS WITH {
  SELECT DISTINCT * WHERE {
    INCLUDE %SCHOLARS
    ?scholar rdfs:label ?scholarLabel . FILTER(LANG(?scholarLabel) = "en")
    ?subject rdfs:label ?subjectLabel . FILTER(LANG(?subjectLabel) = "en")
  }
} AS %LABELED WITH {
  SELECT DISTINCT ?scholar ?scholarLabel (GROUP_CONCAT(DISTINCT ?subjectLabel; separator=", ") as ?subjects)
                  (COUNT(DISTINCT ?subject) AS ?count)
                  (COUNT(DISTINCT ?output) AS ?works)
                  (COUNT(DISTINCT ?award) AS ?awards) WHERE {
    INCLUDE %LABELED
    OPTIONAL { ?scholar wdt:P166 ?award }
    OPTIONAL { ?scholar ^wdt:P50 ?output }
  } GROUP BY ?scholar ?scholarLabel 
} AS %ALL WHERE {
  INCLUDE %ALL
  FILTER(?count >= 2)
  FILTER CONTAINS(?subjects, "proteomics")
} ORDER BY DESC(?works) DESC(?awards) DESC(?count)